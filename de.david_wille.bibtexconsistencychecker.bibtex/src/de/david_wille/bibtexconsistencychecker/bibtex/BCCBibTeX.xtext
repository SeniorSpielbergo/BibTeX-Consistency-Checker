grammar de.david_wille.bibtexconsistencychecker.bibtex.BCCBibTeX hidden(WS, SL_COMMENT)

generate bCCBibTeX "http://www.david-wille.de/bibtexconsistencychecker/bibtex/BCCBibTeX"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

BCCBibTeXFile:
	(entries += BCCAbstractBibTeXFileEntry)*;

BCCAbstractBibTeXFileEntry:
	BCCAbstractBibTeXEntry | BCCReplacePatternEntry;

BCCReplacePatternEntry:
	'@string' '{' replaceKey = BCCReplaceKey '=' replacementFieldValue = BCCReplacementFieldValue '}';

BCCAbstractBibTeXEntry:
	BCCInProceedingsEntry | BCCBookEntry | BCCArticleEntry | BCCStandardEntry |
	BCCInBookEntry | BCCReportEntry | BCCThesisEntry | BCCInCollectionEntry | BCCProceedingsEntry |
	BCCBookletEntry | BCCConferenceEntry | BCCManualEntry | BCCMasterthesisEntry | BCCMiscEntry |
	BCCPhdThesisEntry | BCCTechReportEntry | BCCUnpublishedEntry;

BCCInProceedingsEntry:
	'@inproceedings' entryBody = BCCEntryBody;

BCCBookEntry:
	'@book' entryBody = BCCEntryBody;

BCCBookletEntry:
	'@booklet' entryBody = BCCEntryBody;

BCCConferenceEntry:
	'@conference' entryBody = BCCEntryBody;

BCCManualEntry:
	'@manual' entryBody = BCCEntryBody;

BCCMasterthesisEntry:
	'@masterthesis' entryBody = BCCEntryBody;

BCCMiscEntry:
	'@misc' entryBody = BCCEntryBody;

BCCPhdThesisEntry:
	'@phdthesis' entryBody = BCCEntryBody;

BCCArticleEntry:
	'@article' entryBody = BCCEntryBody;

BCCStandardEntry:
	'@standard' entryBody = BCCEntryBody;

BCCInBookEntry:
	'@inbook' entryBody = BCCEntryBody;

BCCReportEntry:
	'@report' entryBody = BCCEntryBody;

BCCTechReportEntry:
	'@techreport' entryBody = BCCEntryBody;

BCCUnpublishedEntry:
	'@unpublished' entryBody = BCCEntryBody;

BCCThesisEntry:
	'@thesis' entryBody = BCCEntryBody;

BCCInCollectionEntry:
	'@incollection' entryBody = BCCEntryBody;

BCCProceedingsEntry:
	'@proceedings' entryBody = BCCEntryBody;

BCCEntryBody:
	'{' entryKey = BCCEntryKey
		(',' fields += BCCAbstractEntryBodyField)*
	'}';

BCCAbstractEntryBodyField:
	BCCAbstractPersonField | BCCAbstractGenericField | BCCMonthField | BCCDateField | BCCYearField |
	BCCPagesField;

BCCAbstractGenericField:
	BCCTitleField | BCCEditionField | BCCUrlField | BCCInstitutionField | BCCOrganizationField | BCCRevisionField | BCCIsbnField |
	BCCDoiField | BCCAddressField | BCCTypeField | BCCNumberField | BCCVolumeField | BCCKeywordsField | BCCNoteField | BCCPublisherField |
	BCCJournalField | BCCBooktitleField | BCCSchoolField | BCCSeriesField | BCCChapterField | BCCVersionField | BCCHowPublishedField;

BCCTitleField:
	'title' '=' fieldValue = BCCGenericFieldValue;

BCCEditionField:
	'edition' '=' fieldValue = BCCGenericFieldValue;

BCCInstitutionField:
	'institution' '=' fieldValue = BCCGenericFieldValue;

BCCOrganizationField:
	'organization' '=' fieldValue = BCCGenericFieldValue;

BCCRevisionField:
	'revision' '=' fieldValue = BCCGenericFieldValue;

BCCUrlField:
	'url' '=' fieldValue = BCCGenericFieldValue;

BCCDoiField:
	'doi' '=' fieldValue = BCCGenericFieldValue;

BCCTypeField:
	'type' '=' fieldValue = BCCGenericFieldValue;

BCCIsbnField:
	'isbn' '=' fieldValue = BCCGenericFieldValue;

BCCAddressField:
	'address' '=' fieldValue = BCCGenericFieldValue;

BCCHowPublishedField:	
	'howpublished' '=' fieldValue = BCCGenericFieldValue;

BCCNumberField:
	'number' '=' fieldValue = BCCGenericFieldValue;

BCCVolumeField:
	'volume' '=' fieldValue = BCCGenericFieldValue;

BCCNoteField:
	'note' '=' fieldValue = BCCGenericFieldValue;

BCCSchoolField:
	'school' '=' fieldValue = BCCGenericFieldValue;

BCCKeywordsField:
	'keywords' '=' fieldValue = BCCGenericFieldValue;

BCCBooktitleField:
	'booktitle' '=' fieldValue = BCCGenericFieldValue;

BCCSeriesField:
	'series' '=' fieldValue = BCCGenericFieldValue;

BCCPublisherField:
	'publisher' '=' fieldValue = BCCGenericFieldValue;

BCCJournalField:
	'journal' '=' fieldValue = BCCGenericFieldValue;

BCCChapterField:
	'chapter' '=' fieldValue = BCCGenericFieldValue;

BCCVersionField:
	'version' '=' fieldValue = BCCGenericFieldValue;

BCCAbstractPersonField:
	BCCAuthorField | BCCEditorField;

BCCAuthorField:
	'author' '=' personFieldBody = BCCPersonFieldBody;

BCCEditorField:
	'editor' '=' personFieldBody = BCCPersonFieldBody;

BCCYearField:
	'year' '=' '{' year = INT '}';

BCCDateField:
	'date' '=' '{' date = INT '}';

BCCMonthField:
	'month' '=' '{' month = INT '}';

BCCPagesField:
	'pages' '=' '{' pages = BCCPages '}';

BCCPages hidden():
	startPage = BCCPagesValue '--' endPage = BCCPagesValue;

BCCPersonFieldBody:
	'{' persons += BCCPerson ('and' persons += BCCPerson)* '}';

BCCPerson:
	lastName = BCCNameValue (',' firstName = BCCNameValue)?;

BCCPagesValue:
	INT (':' | INT)*;

BCCGenericFieldValue:
	(BCCValidID | INT | BCCSpecialCharacterWithCommaWithQuote | BCCBracket | BCCEscapedSpecialCharacter | BCCParenthesizedFieldValue)*;

BCCParenthesizedFieldValue:
	'{' BCCGenericFieldValue '}';

BCCReplacementFieldValue:
	'"' BCCReplacementFieldValueContents '"';

BCCReplacementFieldValueContents:
	(BCCValidID | INT | BCCSpecialCharacterWithComma | BCCBracket | BCCEscapedSpecialCharacter | BCCParenthesizedReplacementFieldValue)*;

BCCParenthesizedReplacementFieldValue:
	'{' BCCReplacementFieldValueContents '}';
	
BCCNameValue:
	(BCCBracket | BCCEscapedSpecialCharacter | BCCSpecialCharacter | BCCParenthesizedNameValue | ID)*;

BCCParenthesizedNameValue:
	'{' BCCNameValue '}';

BCCSpecialCharacter:
	'!' | '?' | '#' | '$' | '*' | '~' | '/' | '.' | '+' | '-' | '--' | '_' | '//' | ';' | ':' | '^' | '@';

BCCSpecialCharacterWithComma:
	BCCSpecialCharacter | ',';

BCCSpecialCharacterWithCommaWithQuote:
	BCCSpecialCharacter | ',' | '"';

BCCEscapedSpecialCharacter:
	'\\\"' | '\\\'' | '\\`'| '\\~' | '\\Â´' | '\\^' | '\'' | '\\&' | '\\-' | '\\';

BCCBracket:
	'(' | ')' | '[' | ']';

BCCEntryKey:
	ID (':' | '+' | '-' | ID | INT)*;

BCCReplaceKey:
	ID (':' | '_' | '-' | '+' | INT | ID)*;

BCCValidID:
	ID | 'and' | 'title' | 'edition' | 'institution' | 'organization' | 'revision' | 'url' |
	'doi' | 'type' | 'isbn' | 'address' | 'howpublished' | 'number' | 'volume' | 'note' |
	'school' | 'keywords' | 'booktitle' | 'series' | 'publisher' | 'journal' | 'chapter' |
	'version' | 'author' | 'editor' | 'year' | 'date' | 'month' | 'pages';

terminal ID:
	('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal INT returns ecore::EInt:
	('0'..'9')+;

terminal SL_COMMENT:
	'%' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' '|'\t'|'\r'|'\n')+;

grammar de.david_wille.bibtexconsistencychecker.executionmodel.BCCExecutionModel with org.eclipse.xtext.common.Terminals

generate bCCExecutionModel "http://www.david-wille.de/bibtexconsistencychecker/executionmodel/BCCExecutionModel"

BCCExecutionModel:
	settingsEntry = BCCSettingsEntry
	(rulesEntry = BCCConsistencyRulesEntry)? &
	(bibTeXFilesEntry = BCCBibTeXFilesEntry)?;

BCCSettingsEntry:
	'settings' '{'
		'name' name = STRING
		('ensure' '{'
			((ensureAlphbeticOrderActivated ?= BCCAlphabeticOrderKeyword)? &
			(ensureShortHarvardStyleActivated ?= BCCShortHarvardStyleKeyword)? &
			(ensureNoConflictingEntryKeysExistActivated ?= BCCNoConflictingEntryKeysExistKeyword)? &
			(ensureReplacePatternExistActivated ?= BCCReplacePatternExistKeyword)? &
			(ensureNoContradictingReplacePatternExistActivated ?= BCCNoContradictingReplacePatternExistKeyword)?)
		'}')?
	'}';

BCCConsistencyRulesEntry:
	BCCConsistencyRulesKeyword '{'
		(consistencyRuleEntries += BCCConsistencyRuleEntry)+
	'}';

BCCBibTeXFilesEntry:
	BCCBibTeXFilesKeyword '{'
		(bibTeXFileEntries += BCCBibTeXFileEntry)+
	'}';

BCCConsistencyRuleEntry:
	'rule' rulePath = BCCRulePath;

BCCBibTeXFileEntry:
	'file' filePath = BCCBibTeXPath;

BCCRulePath:
	path = RULE_PATH;

BCCBibTeXPath:
	path = BIBTEX_PATH;

terminal RULE_PATH:
	('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'-'|'_'|'0'..'9')* '.bcc_rule';

terminal BIBTEX_PATH:
	('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'-'|'_'|'0'..'9')* '.bib';

BCCAlphabeticOrderKeyword:
	'alphabetic' 'order';

BCCShortHarvardStyleKeyword:
	'short' 'harvard' 'style';

BCCReplacePatternExistKeyword:
	'replace' 'pattern' 'exist';

BCCNoContradictingReplacePatternExistKeyword:
	'no' 'contradicting' 'replace' 'pattern' 'exist';

BCCNoConflictingEntryKeysExistKeyword:
	'no' 'conflicting' 'entry' 'keys' 'exist';

BCCConsistencyRulesKeyword:
	'consistency' 'rules';

BCCBibTeXFilesKeyword:
	'bibtex' 'files';

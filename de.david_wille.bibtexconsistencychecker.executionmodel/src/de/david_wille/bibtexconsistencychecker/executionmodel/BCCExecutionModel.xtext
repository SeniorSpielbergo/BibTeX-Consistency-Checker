grammar de.david_wille.bibtexconsistencychecker.executionmodel.BCCExecutionModel with org.eclipse.xtext.common.Terminals

generate bCCExecutionModel "http://www.david-wille.de/bibtexconsistencychecker/executionmodel/BCCExecutionModel"

BCCExecutionModel:
	settingsEntry = BCCSettingsEntry
	(rulesEntry = BCCConsistencyRulesEntry)? &
	(bibTeXFilesEntry = BCCBibTeXFilesEntry)?;

BCCSettingsEntry:
	'settings' '{'
		'name' name = STRING
		('ensure' '{'
			(ensureAlphbeticOrderActivated ?= AlphabeticOrderKeyword)? &
			(ensureShortHarvardStyleActivated ?= ShortHarvardStyleKeyword)? &
			(ensureReplacePatternExistActivated ?= ReplacePatternExistKeyword)? &
			(ensureNoContradictingReplacePatternExistActivated ?= NoContradictingReplacePatternExistKeyword)?
		'}')?
	'}';

BCCConsistencyRulesEntry:
	ConsistencyRulesKeyword '{'
		(consistencyRuleEntries += BCCConsistencyRuleEntry)+
	'}';

BCCBibTeXFilesEntry:
	BibTeXFilesKeyword '{'
		(bibTeXFileEntries += BCCBibTeXFileEntry)+
	'}';

BCCConsistencyRuleEntry:
	'rule' rulePath = BCCRulePath;

BCCBibTeXFileEntry:
	'file' filePath = BCCBibTeXPath;

BCCRulePath:
	path = RULE_PATH;

BCCBibTeXPath:
	path = BIBTEX_PATH;

terminal RULE_PATH:
	('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'-'|'_'|'0'..'9')* '.bcc_rule';

terminal BIBTEX_PATH:
	('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'-'|'_'|'0'..'9')* '.bib';

AlphabeticOrderKeyword:
	'alphabetic' 'order';

ShortHarvardStyleKeyword:
	'short' 'harvard' 'style';

ReplacePatternExistKeyword:
	'replace' 'pattern' 'exist';

NoContradictingReplacePatternExistKeyword:
	'no' 'contradicting' 'replace' 'pattern' 'exist';

ConsistencyRulesKeyword:
	'consistency' 'rules';

BibTeXFilesKeyword:
	'bibtex' 'files';
